OnTranslate
{
	_talk = reference0
	
	//This is what makes %(embedded_elements) work in script input
	if reference1 == "" && reference2 == "" //If this is from the input box
	{ //send input box : no event (ref2) , no special flag (ref1)
		_talk = EVAL('"' + REPLACE(_talk,'"','""') + '"')
	}
	
	_bloon = 0
	case CurrentColor
	{
		when "Red"; _bloon = 1
		when "Green"; _bloon = 2
		when "Black"; _bloon = 3
		when "Purple"; _bloon = 4
	}
	
	if SHIORI3FW.BalloonName == "Noodloon"
	{
		_talk = REPLACE(_talk,"\0\b[0]","\0\b[%(_bloon)0]")
		_talk = REPLACE(_talk,"\0\b[2]","\0\b[%(_bloon)2]")
		_talk = REPLACE(_talk,"\0\b[4]","\0\b[%(_bloon)4]")
	}
	
	TOSTR(_talk)
}

OnTranslateInternal.AutoPauses
{
	_talk = _argv[0]
	
	if "\![no-autopause]" !_in_ _talk
	{
		/*
		//Additional options if you want ellipsis to have little pauses between each dot. Personally, I think I want to do that manually, for when I want a particular flavor.
		
		_talk = REPLACE(_talk,"... ",".\w2.\w2.\w8 ") //Ellipsis
		_talk = REPLACE(_talk,"..? ",".\w2.\w2?\w8 ") //Question mark + ellipsis
		_talk = REPLACE(_talk,"..! ",".\w2.\w2!\w8 ") //Exclamation point + ellipsis
		*/
		
		_talk = REPLACE(_talk,", ",",\w4 ") //Comma
		_talk = REPLACE(_talk,". ",".\w8 ") //Period
		_talk = REPLACE(_talk,"? ","?\w8 ") //Question mark
		_talk = REPLACE(_talk,"! ","!\w8 ") //Exclamation point
		//_talk = REPLACE(_talk,"- ","-\w8 ") //Dash - This one seems to happen a lot when I don't want it, so I am commenting it out
		_talk = REPLACE(_talk,"~ ","~\w8 ") //Tilde
		_talk = REPLACE(_talk,"; ",";\w4 ") //Semicolon
		_talk = REPLACE(_talk,": ",";\w8 ") //Colon
	}
	
	_talk
}

OnAiTalk
{
	if CHAIN.IDName == ""
	{
		LastTalk = RandomTalk
	}
	else
	{
		LastTalk = ChainTalk
	}
	LastTalk
}


//This is just here so you can use anchors to link to websites if you want. You can link to websites with normal menu choice tags! If you do, a handy URL preview will show up in the balloon!
OnAnchorSelect
{
	if "http://" _in_ reference0 || "https://" _in_ reference0; "\j[""%(reference0)""]"
}

//Hotkeys
OnKeyPress
{
	if reference0 == "f1"; "\![open,readme]"
	elseif reference0 == "t"; OnAiTalk
	elseif reference0 == "r"; OnLastTalk
}

//Happens about 30 seconds after a script ends, by default, to bring them back to normal poses
OnSurfaceRestore
{
	"\1\s[-1]\0\s[0]"
}

//Happens when the ghost is unminimized. You *can* include dialogue, but you don't *have* to. I have included this event because if you don't, it defaults to \0\s[0]\1\s[10], which you don't want in some cases, such as if your ghost uses different surfaces or has a different number of characters.
OnWindowStateRestore
{
	"\1\s[-1]\0\s[0]"
}

//Network update url. Make sure you have a / on the end of the url or it will give a warning every time users try to update! If you don't plan to use network updates you can remove this
//IF YOU'RE USING GITHUB FOR NETWORK UPDATES: DO NOT USE github.io URLS! It will cause you pain and suffering. USE raw.githubusercontent!!!
//To get the raw.githubusercontent link super easy:
//• Open your ghost's repo online
//• navigate to the ghost's folder (the top level one, with install.txt, readme.txt, etc)
//• Click on install.txt, or another text file on that level
//• Once on that page, click the "raw" button. You should see the url now starts with raw.githubusercontent
//• Copy that url exactly as is, but *leave off the "install.txt" or whatever file name at the end.*
//Tada! That's your url! Make sure you test it! The dev palette and script/network log can help with that
On_homeurl
{
	""
}


//—————————————————————————————— Right click menu links ——————————————————————————————

//Takes an array of alternating names and urls, and formats them for use with the right click menu
FormatLinks : all
{
	for _i = 0; _i < ARRAYSIZE(_argv); _i++
	{
		_argv[_i] //Add the link/title
		//Alternate between adding %ASC(1) or %ASC(2)
		if _i % 2 == 1; "%ASC(2)"
		else; "%ASC(1)"
	}
}

On_sakura.recommendsites
{
	FormatLinks(recommendsites_sakura)
}

//These are the websites linked in the top option of the sakura's right click menu. The format is simple; write the label of the link on the left followed by a semicolon, and then on the right, the url to link to. These should be separate strings.
recommendsites_sakura : array
{
	"Link label";	"https://link.example.com/link/to/whatever/here/"
}

On_sakura.portalsites 
{
	FormatLinks(portalsites_sakura)
}

//These are the websites linked in the second option of the sakura's right click menu. Same as above
portalsites_sakura : array
{
	"Another link label";	"https://link.example.com/link/to/whatever/here/again/"
	"A third link label";	"https://link.example.com/link/to/whatever/a/third/time/you/can/add/as/many/as/you/want/"
}

On_kero.recommendsites
{
	FormatLinks(recommendsites_kero)
}

//These are the websites linked in the first option of the kero's right click menu. Same as above
recommendsites_kero : array
{
	"These labels can be anything you want";	"https://link.example.com/link/to/whatever/here/once/more/"
}


//—————————————————————————————— User pronoun related ——————————————————————————————

they
{
	if SHIORI3FW.UserSex == "male"; "he"
	elseif SHIORI3FW.UserSex == "female"; "she"
	else; "they"
}

them
{
	if SHIORI3FW.UserSex == "male"; "him"
	elseif SHIORI3FW.UserSex == "female"; "her"
	else; "them"
}

their
{
	if SHIORI3FW.UserSex == "male"; "his"
	elseif SHIORI3FW.UserSex == "female"; "her"
	else; "their"
}

theirs
{
	if SHIORI3FW.UserSex == "male"; "his"
	elseif SHIORI3FW.UserSex == "female"; "hers"
	else; "theirs"
}

themself
{
	if SHIORI3FW.UserSex == "male"; "himself"
	elseif SHIORI3FW.UserSex == "female"; "herself"
	else; "themself"
}

//%(they) %(p('are','is'))
//You can change the name of this function if you want! I kept it short so it's fast to type
p
{
	if SHIORI3FW.UserSex == "undef"; _argv[0] //they are
	else; _argv[1] //he is/she is
}

/*

An example of the above setup:
"%(they) %(p('were','was')) looking for that earlier, I'll ask if %(they)'%('ve','s') found it."

If using they, the %(p) functions will return the first word specified, resulting in:
"they were looking for that earlier, I'll ask if they've found it."

If not using they, for example if using she, it will return the second specified word instead, resulting in:
"she was looking for that earlier, I'll ask if she's found it."

With this function, you can quickly customize words that need to change based on whether it should be singular or plural. Just remember; the %(p) function is set up so that the first word you specify matches with the 'they' labels used on the pronoun functions. You can always switch that around if you like, too.

*/

//Copied from the base SHIORI files, but with the username variable added
OnNotifyUserInfo : void
{
	SHIORI3FW.UserName = reference[0]
	username = reference[0]
	SHIORI3FW.UserNameFull = reference[1]
	SHIORI3FW.UserBirthday = SPLIT(reference[2],',',3)
	for _i = 0 ; _i < 3 ; _i++ {
		SHIORI3FW.UserBirthday[_i] = TOINT(SHIORI3FW.UserBirthday[_i])
	}
	SHIORI3FW.UserSex = reference[3]
}

OnNotifyDressupInfo
{
	CurrentColor = ""
	//character ID, category name, part name, option, on-1/off-0, thumbnail path
	foreach reference; _ref
	{
		if _ref[4,C_BYTE1] == "1"
		{
			CurrentColor = _ref[2,C_BYTE1]
			break
		}
	}
}

br
{
	if SHIORI3FW.BalloonName == "Noodloon"; "\n"
	else; " "
}


On_getaistateex : all
{
	if reference0 == 0; "%(OUTPUTNUM('RandomTalk')),%(OUTPUTNUM('hidingspot')),%(OUTPUTNUM('bodypart')),%(OUTPUTNUM('badenvironment')),%(OUTPUTNUM('considered')),%(OUTPUTNUM('pasta'))" + C_BYTE1
	// if reference0 == 1; "%(OUTPUTNUM('that')),%(OUTPUTNUM('doer_of_things')),%(OUTPUTNUM('thinkabout')),%(OUTPUTNUM('bodypart')),%(OUTPUTNUM('name1')),%(OUTPUTNUM('name2'))" + C_BYTE1
	// if reference0 == 2; "%(OUTPUTNUM('creature')),%(OUTPUTNUM('object')),%(OUTPUTNUM('food')),%(OUTPUTNUM('flavor')),%(OUTPUTNUM('cuts')),%(OUTPUTNUM('bodypart')),processed" + C_BYTE1

	if reference0 == 0; "RandomTalk,hidingspot,bodypart,badenvironment,considered,pasta"
	// if reference0 == 1; "that,doer_of_things,thinkabout,bodypart,name1,name2"
	// if reference0 == 2; "creature,object,food,flavor,cuts,processed"
}